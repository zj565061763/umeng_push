plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def libGroupId = "com.sd.lib.umeng"
def libArtifactId = "umeng_push"
def libVersionName = "6.5.4-v02"

android {
    compileSdk 32
    defaultConfig {
        minSdk 21
        targetSdk 32
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api 'com.github.zj565061763:umeng_common:9.5.2-v02'
    api 'com.umeng.umsdk:push:6.5.4'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = libGroupId
                artifactId = libArtifactId
                version = libVersionName

                // 上传源码
                artifact generateSourcesJar
            }
        }
    }
}